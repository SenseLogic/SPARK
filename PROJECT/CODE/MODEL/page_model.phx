// -- IMPORTS

import 'page.php';

// -- FUNCTIONS

function LinkPageArray(
    array &page_array
    )
{
    var page_count = count( page_array );
    var page_index = 0;

    foreach ( var page; page_array )
    {
        page.PageIndex = page_index;

        if ( page_index === 0 )
        {
            page.PriorPage = page_array[ page_count - 1 ];
        }
        else
        {
            page.PriorPage = page_array[ page_index - 1 ];
        }

        if ( page_index === page_count - 1 )
        {
            page.NextPage = page_array[ 0 ];
        }
        else
        {
            page.NextPage = page_array[ page_index + 1 ];
        }

        ++page_index;
    }
}

// ~~

function GetBlockComparison(
    object &first_block,
    object &second_block
    )
{
    return first_block.Number <=> second_block.Number;
}

// ~~

function GetActivePageArray(
    array &page_array,
    string language_code
    )
{
    var active_page_array = [];

    foreach ( var page; page_array )
    {
        if ( page.IsActive
             && in_array( language_code, page.LanguageCodeArray, true ) )
        {
            array_push( active_page_array, page );
        }
    }

    return active_page_array;
}

// ~~

function GetValidPageByIdMap(
    array &page_array,
    array &block_array,
    array &block_by_id_map
    )
{
    var page_by_id_map = [];

    foreach ( var page; page_array )
    {
        page.BlockArray = [];
        page_by_id_map[ page.Id ] = page;
    }

    foreach ( var block; block_array )
    {
        if ( isset( page_by_id_map[ block.PageId ] ) )
        {
            array_push(
                page_by_id_map[ block.PageId ].BlockArray,
                block
                );
        }
    }

    foreach ( var page; page_array )
    {
        SortArrayByValue( page.BlockArray, 'GetBlockComparison' );

        page.HeadingBlockArray = GetBlockArrayByCategorySlug( page.BlockArray, 'heading' );
        page.ContentBlockArray = GetBlockArrayByCategorySlug( page.BlockArray, 'content' );
    }

    foreach ( var page; page_array )
    {
        LinkBlockArray( page.HeadingBlockArray );
        LinkBlockArray( page.ContentBlockArray );
    }

    return page_by_id_map;
}

// ~~

function GetValidPageById(
    array &page_by_id_map,
    string id
    )
{
    foreach ( var page_id => var page; page_by_id_map )
    {
        if ( page.Id === id )
        {
            return page;
        }
    }

    return null;
}

// ~~

function GetPageBySlugMap(
    array &page_array
    )
{
    var page_by_slug_map = [];

    foreach ( var page; page_array )
    {
        page_by_slug_map[ page.Slug ] = page;
    }

    return page_by_slug_map;
}

// ~~

function GetValidPageByIdOrSlug(
    array &page_by_id_map,
    array &page_by_slug_map,
    string id_or_slug
    )
{
    if ( HasKey( page_by_id_map, id_or_slug ) )
    {
        return page_by_id_map[ id_or_slug ];
    }
    else if ( HasKey( page_by_slug_map, id_or_slug ) )
    {
        return page_by_slug_map[ id_or_slug ];
    }
    else
    {
        return null;
    }
}
