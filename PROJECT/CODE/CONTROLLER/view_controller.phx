// -- IMPORTS

import 'controller.php';
import '../MODEL/session.php';
import '../MODEL/text_model.php';

// -- TYPES

class VIEW_CONTROLLER extends CONTROLLER
{
    // -- ATTRIBUTES

    attribute
        BrowserAddress,
        BrowserLocation,
        LanguageTag,
        TextArray,
        TextBySlugMap;

    // -- CONSTRUCTORS

    method constructor(
        string language_code
        )
    {
        parent::constructor( language_code );

        if ( HasQueryValue( 'testip' ) )
        {
            .BrowserAddress = GetQueryValue( 'testip' );
        }
        else
        {
            .BrowserAddress = GetBrowserAddress();
        }

        if ( HasQueryValue( 'testcountry' ) )
        {
            .BrowserLocation = GetCountryLocation( GetQueryValue( 'testcountry' ) );
        }
        else
        {
            .BrowserLocation = GetBrowserLocation( .BrowserAddress );
        }

        .LanguageTag = language_code .. '-' .. .BrowserLocation.CountryCode .. '/' .. .BrowserLocation.ContinentSlug;
        .TextArray = GetDatabaseTextArray();
        .TextBySlugMap = GetTextBySlugMap( .TextArray );
    }

    // -- INQUIRIES

    method GetTranslatedNumberBySlug(
        string slug
        )
    {
        return .GetTranslatedNumber( .TextBySlugMap[ slug ] );
    }

    // ~~

    method GetProcessedTextBySlug(
        string slug
        )
    {
        return .GetProcessedText( .TextBySlugMap[ slug ] );
    }

    // ~~

    method GetProcessedMultilineTextBySlug(
        string slug
        )
    {
        return .GetProcessedMultilineText( .TextBySlugMap[ slug ] );
    }
}
