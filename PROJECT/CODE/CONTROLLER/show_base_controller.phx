// -- IMPORTS

import 'view_controller.php';
import '../MODEL/block_model.php';
import '../MODEL/language_model.php';
import '../MODEL/page_model.php';

// -- TYPES

class SHOW_BASE_CONTROLLER extends VIEW_CONTROLLER
{
    // -- ATTRIBUTES

    attribute
        Route,
        LanguageArray,
        PageArray,
        BlockArray,
        LanguageByCodeMap,
        Language,
        BlockByIdMap,
        PageByIdMap,
        PageByRouteMap,
        PageArrayByTypeSlugMap,
        ImagePathArray,
        Captcha,
        Session;

    // -- CONSTRUCTORS

    method constructor(
        string language_code,
        string route
        )
    {
        parent::constructor( language_code );

        .Route = route;

        .LanguageArray = GetDatabaseLanguageArray();
        .LanguageArray = GetActiveLanguageArray( .LanguageArray );
        .PageArray = GetDatabasePageArray();
        .PageArray = GetActivePageArray( .PageArray, language_code );
        .BlockArray = GetDatabaseBlockArray();
        .BlockArray = GetActiveBlockArray( .BlockArray, language_code );

        .LanguageByCodeMap = GetLanguageByCodeMap( .LanguageArray );
        .Language = GetLanguageByCode( .LanguageByCodeMap, language_code );

        .BlockArray = array_merge( .BlockArray );
        .BlockByIdMap = GetValidBlockByIdMap( .BlockArray );

        .PageArray = array_merge( .PageArray );
        .PageByIdMap = GetValidPageByIdMap( .PageArray, .BlockArray, .BlockByIdMap );
        .PageByRouteMap = GetPageByRouteMap( .PageArray );
        .PageArrayByTypeSlugMap = GetPageArrayByTypeSlugMap( .PageArray );

        .ImagePathArray = [];

        foreach ( var page_id => var page; .PageByIdMap )
        {
            if ( property_exists( page, 'ImagePath' )
                 && page.ImagePath !== '' )
            {
                array_push( .ImagePathArray, page.ImagePath );
            }
        }

        foreach ( var block_id => var block; .BlockByIdMap )
        {
            if ( property_exists( block, 'ImagePath' )
                 && block.ImagePath !== '' )
            {
                array_push( .ImagePathArray, block.ImagePath );
            }
        }

        .Captcha = GetCaptchaText( 6 );
        .Session.Captcha = .Captcha;
        .Session.Store();

        import '../VIEW/show_base_view.php';
    }
}

// -- STATEMENTS

var show_base_controller = new SHOW_BASE_CONTROLLER( var language_code, var route );
