// -- FUNCTIONS

function GetDatabaseConnection(
    )
{
    static local
        connection = null;

    if ( is_null( connection ) )
    {
        connection
            = new PDO(
                  'mysql:host=' .. DatabaseHost .. ';dbname=' .. DatabaseName,
                  DatabaseUserName,
                  DatabasePassword
                  );

        connection.prepare( "set names 'utf8mb4'" ).execute();
    }

    return connection;
}

// ~~

function GetDatabaseError(
    )
{
    return GetDatabaseConnection().errorInfo();
}

// ~~

function GetDatabaseAddedId(
    )
{
    return GetDatabaseConnection().lastInsertId();
}

// ~~

function GetDatabaseStatement(
    string command
    )
{
    return GetDatabaseConnection().prepare( command );
}

// ~~

function RunDatabaseCommand(
    string command
    )
{
    var statement = GetDatabaseStatement( command );

    if ( !statement.execute() )
    {
        var_dump( statement.errorInfo() );
    }

    return statement;
}

// ~~

function GetDatabaseObject(
    statement
    )
{
    return statement.fetchObject();
}

// ~~

function GetDatabaseObjectArray(
    statement
    )
{
    var object_array = [];

    while ( var object = statement.fetchObject() )
    {
        array_push( object_array, object );
    }

    return object_array;
}

// ~~

function GetDatabaseTableObjectArray(
    string table_name
    )
{
    var statement = GetDatabaseStatement( 'select * from `' .. table_name .. '`' );

    if ( !statement.execute() )
    {
        var_dump( statement.errorInfo() );
    }

    return GetDatabaseObjectArray( statement );
}

// ~~

function GetDatabaseTableNameArray(
    )
{
    var statement = GetDatabaseStatement( 'show tables' );

    if ( !statement.execute() )
    {
        var_dump( statement.errorInfo() );
    }

    var table_name_array = [];

    while ( var column_array = statement.fetch( PDO::FETCH_NUM ) )
    {
        array_push( table_name_array, column_array[ 0 ] );
    }

    sort( table_name_array );

    return table_name_array;
}

// ~~

function GetDatabaseColumnArray(
    string table_name
    )
{
    var statement
        = GetDatabaseStatement(
            "select * from information_schema.columns where table_schema = '"
            .. DatabaseName
            .. "' and table_name = '"
            .. table_name
            .. "' order by ordinal_position asc"
            );

    if ( !statement.execute() )
    {
        var_dump( statement.errorInfo() );
    }

    var column_object_array = GetDatabaseObjectArray( statement );
    var column_array = [];

    foreach ( var column_object; column_object_array )
    {
        var column = new stdClass();
        column.Number = column_object.ORDINAL_POSITION;
        column.Name = column_object.COLUMN_NAME;
        column.Type = column_object.COLUMN_TYPE;
        column.DataType = column_object.DATA_TYPE;
        column.Capacity = column_object.CHARACTER_MAXIMUM_LENGTH;
        column.Precision = column_object.NUMERIC_PRECISION;
        column.Default = column_object.COLUMN_DEFAULT;
        column.IsNullable = ( column_object.IS_NULLABLE === 'YES' );
        column.IsPrimaryKey = ( column_object.COLUMN_KEY === 'PRI' );

        array_push( column_array, column );
    }

    return column_array;
}

// ~~

function GetDatabaseColumnByNameMap(
    array &column_array
    )
{
    var column_by_name_map = [];

    foreach ( var column; column_array )
    {
        column_by_name_map[ column.Name ] = column;
    }

    return column_by_name_map;
}
